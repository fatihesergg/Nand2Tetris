// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=outzx); // if zx == 1 then set x = 0
    
    Not16(in=outzx,out=notX); // not x
    
    Mux16(a=outzx,b=notX,sel=nx,out=outnx); // if nx == 1 then set x = !x

    Mux16(a=y,b=false,sel=zy,out=outzy); //if zy == 1 then set y = 0
    
    Not16(in=outzy,out=notY); // not y
    
    Mux16(a=outzy,b=notY,sel=ny,out=outny); // if ny == 1 then set y = !y

    Add16(a=outnx,b=outny,out=outAdd); // x + y
    
    And16(a=outnx,b=outny,out=outAnd); // x & y

    Mux16(a=outAnd,b=outAdd,sel=f,out=outf); // if sel == 1 then set out x+y 
    
    Not16(in=outf,out=outfnot); // not outf
    
    Mux16(a=outf,b=outfnot,sel=no,out=out,out=lastout);

    Or16Way(in=lastout, out=orOut);
    Not(in=orOut,out=zr); //zr

    IsNg(in=lastout,out=ng);
    
}